# Tell 'just' to run bash, source our setup script, then execute the recipe
set shell := ["bash", "-c"]

version := `sed -n 's/.*VERSION = "\(.*\)"/\1/p' lib/dry/cli/ui/version.rb`
rbenv   := 'eval "$(rbenv init - bash 2>/dev/null || true)"; bundle exec '
repo    := 'git@github.com:kigster/dry-cli-ui.git'

gem_name := 'dry-cli-ui'
gem_file := 'pkg/' + gem_name + '-' + version + '.gem'
gem_url  := 'https://rubygems.org/gems/' + gem_name

[no-exit-message]
recipes:
    just --choose

# Sync all dependencies
install:
    bin/setup

build: install

# Lint Ruby files
lint:
    {{ rbenv }} rubocop

# Autocorrect Ruby files (pass -A for unsafe corrections) and reformat markdown
format *args:
    {{ rbenv }} rubocop -a {{ args }}
    just format-markdown

# Reformat every markdown file
format-markdown:
    fd -e md -X mdformat --wrap no

# Run all the tests
test *args:
    export ENVIRONMENT=test; {{ rbenv }} rspec {{ args }}

# Run tests and enforce 100% line and branch coverage
test-coverage *args:
    export ENVIRONMENT=test; export COVERAGE=true; {{ rbenv }} rspec {{ args }}

ci: lint test-coverage

alias check-all := ci

# Remove build products, coverage and .DS_Store files
clean:
    find . -name .DS_Store -not -path './.git/*' -delete -print
    rm -rf tmp coverage pkg doc .yardoc

# Run all lefthook pre-commit hooks
lefthook:
    lefthook run pre-commit --all-files

# Print current gem version
version:
    @echo "{{ version }}"

# Clobber
clobber:
    {{ rbenv }} rake clobber

# Generate documentation
doc:
    {{ rbenv }} rake doc

# `gem push` rather than `rake release`: release also guards the tree, tags and
# pushes git, which `just release` does deliberately and separately, and it
# gives no way to pass a 2FA code, so it always stopped to prompt.
#
#   just publish            # gem push prompts for the code if 2FA needs one
#   just publish 123456     # use this code
#
# Build the .gem and push it to RubyGems, non-interactively
publish otp="": build
    #!/usr/bin/env bash
    set -euo pipefail
    eval "$(rbenv init - bash 2>/dev/null || true)"

    mkdir -p pkg
    gem build {{ gem_name }}.gemspec --output "{{ gem_file }}"

    otp="{{ otp }}"

    if [[ -n "${otp}" ]]; then
      gem push "{{ gem_file }}" --otp "${otp}"
    else
      echo "rubygems: no OTP given, gem push will prompt if 2FA is required."
      gem push "{{ gem_file }}"
    fi

    # Only reachable when the push succeeded: `set -e` aborts the recipe on a
    # non-zero `gem push`, so the page never opens for a release that failed.
    echo "published {{ gem_name }} {{ version }} to {{ gem_url }}"
    open "{{ gem_url }}" 2>/dev/null || xdg-open "{{ gem_url }}" 2>/dev/null || true

# Tag v{{ version }} and publish the GitHub release
release:
    git fetch --tags
    git tag -f "v{{ version }}"
    git push -f --tags
    gh release delete -y "v{{ version }}" --repo {{ repo }} 2>/dev/null || true
    gh release create "v{{ version }}" --generate-notes --repo {{ repo }}
